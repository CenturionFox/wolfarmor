import groovy.json.JsonBuilder
import groovy.json.JsonSlurper

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "http://repo.spongepowered.org/maven"}
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "org.spongepowered:mixingradle:0.6+"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'groovy'
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven { url = 'http://repo.spongepowered.org/maven' }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/"}
}

def config = null
def apiconfig = null

file "${projectDir.getAbsolutePath()}/config.json" withReader {
    config = new JsonSlurper().parseText(it.text)
}

file "${projectDir.getAbsolutePath()}/wolfarmor-api/config.json" withReader {
    apiconfig = new JsonSlurper().parseText(it.text)
}

def apiVersion = "${apiconfig.version.major}.${apiconfig.version.minor}.${apiconfig.version.patch}"
def shortVersion = "${config.version.major}.${config.version.minor}.${config.version.patch}"

version = "${config.minecraft.version}-${shortVersion}-${config.version.extension}"
group= "dev.satyrn.wolfarmor"
archivesBaseName = "wolfarmor"

println "Current version is ${version}"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
    doFirst {
        copy {
            from sourceSets.main.allJava
            into "$buildDir/generated/main/java"
            filesMatching(["**/WolfArmorMod.java", "**/WolfArmorCore.java"]) {
                expand version: shortVersion, mcversion: config.minecraft.version, modname: config.name, modid: config.modid
            }
        }
        copy {
            from "$buildDir/generated/main/java"
            into "$buildDir/sources/main/java"
        }
    }
}

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}"
    runDir = "run"
    mappings = config.mcp.version
}

dependencies {
    compile project(':wolfarmor-api')
    compile("org.spongepowered:mixin:${config.mixin.version}"){
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
}

sourceSets.main {
    java {
        srcDirs 'src/main/java'
    }
    artifacts {
        archives shadowJar
    }
}

sourceSets.test {
    dependencies {
        testCompile "org.codehaus.groovy:groovy-all:2.5.7"
        testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
        testCompile "junit:junit:4.12"
        testRuntime "org.objenesis:objenesis:2.6"
        testRuntime "net.bytebuddy:byte-buddy:1.9.3"
    }
    java {
        srcDirs 'src/test/groovy'
    }
}

build {
    dependsOn 'signJar'
}

runClient {
    doFirst {
        jvmArgs "-Dfml.coreMods.load=dev.satyrn.wolfarmor.core.WolfArmorCore"
        jvmArgs "-Dmixin.debug=true"
        if (project.hasProperty('username')) {
            args '--username', project.getProperties()['username']
        }
        if (project.hasProperty('password')) {
            args '--password', project.getProperties()['password']
        }
    }
}

jar {
    from sourceSets.main.output
    manifest.attributes(
            'Built-By': System.properties['user.name'],
            'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
            'Implementation-Title': project.name,
            'Implementation-Version': version,
            'Implementation-Vendor': 'https://satyrn.dev',
            'API-Version': apiVersion,
            'FMLCorePlugin': "${project.group}.coremod.WolfArmorCore",
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'TweakOrder': 0,
            'MixinConfigs': 'mixins.wolfarmor.core.json',
            'FMLAT': 'wolfarmor_at.cfg'
    )
}

shadowJar {
    mainSpec.sourcePaths.clear()
    dependsOn reobfJar
    dependencies {
        include dependency('org.spongepowered:mixin')
        include dependency(':wolfarmor-api')
    }

    manifest.attributes(
        'FMLCorePluginContainsFMLMod': true
    )

    exclude 'dummyThing'
    exclude 'LICENSES*'

    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand version: shortVersion, mcversion: config.minecraft.version, modname: config.name, modid: config.modid
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

	// copy LICENSE
    from('/') {
        include 'LICENSE'
        include 'CREDITS'
        include 'THIRDPARTY'
    }
}

sourceTestJava { }

javadoc { }

mixin {
    add sourceSets.main, "mixins.wolfarmor.refmap.json"
}

task signJar(type: SignJar, dependsOn: shadowJar) {
    onlyIf {
        project.hasProperty('keyStore');
    }

    keyStore = project.getProperties()['keyStore'];
    alias = project.getProperties()['keyStoreAlias'];
    storePass = project.getProperties()['keyStorePass'];
    keyPass = project.getProperties()['keyStoreKeyPass'];
    inputFile = shadowJar.archivePath
    outputFile = "$buildDir/libs/$archivesBaseName-$version-signed.jar"
}

task saveConfig {
    doLast {
        def configFile = new File("${projectDir.getAbsolutePath()}/config.json")
        configFile.delete()
        configFile.write(new JsonBuilder(config).toPrettyString())
    }
}

def VERSIONING_GROUP = 'Versioning'
def SCM_GROUP = 'sourcecontrol'

task incrementPatch {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch += 1
        }
    }
}

task incrementMinor {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch = 0
            config.version.minor += 1
        }
    }
}

task incrementMajor {
    group VERSIONING_GROUP
    dependsOn 'gitCheckWorkingTree'
    finalizedBy 'saveConfig'
    finalizedBy 'gitAddConfig'
    finalizedBy 'gitTagVersion'
    finalizedBy 'gitCommit'
    finalizedBy 'reportVersionChange'

    doLast {
        println projectDir.getAbsolutePath()
        if (gitCheckWorkingTree.output() != "") {
            saveConfig.enabled = false
            gitAddConfig.enabled = false
            gitTagVersion.enabled = false
            gitCommit.enabled = false
            throw new Error("Working directory not clean!")
        } else {
            config.version.patch = 0
            config.version.minor = 0
            config.version.major += 1
        }
    }
}

task gitCheckWorkingTree(type:Exec) {
    group SCM_GROUP
    workingDir projectDir.getAbsolutePath()
    executable 'git'
    args '--no-pager', 'diff', 'HEAD'
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
}

task gitAddConfig() {
    group SCM_GROUP
    shouldRunAfter saveConfig
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            ignoreExitValue true
            executable 'git'
            args 'add', 'config.json'
        }
    }
}

task gitTagVersion() {
    group SCM_GROUP
    shouldRunAfter gitAddConfig
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            def newVersionNum = "v${config.version.major}.${config.version.minor}.${config.version.patch}"
            ignoreExitValue true
            executable 'git'
            args 'tag', '-a', "${newVersionNum}", '-m', "\"${newVersionNum}\""
        }
    }
}

task gitCommit() {
    group SCM_GROUP
    shouldRunAfter gitTagVersion
    doLast {
        exec {
            workingDir projectDir.getAbsolutePath()
            def newVersionNum = "v${config.version.major}.${config.version.minor}.${config.version.patch}"
            ignoreExitValue true
            executable 'git'
            args 'commit', '-m', "\"üè∑ ${newVersionNum}\""
        }
    }
}

task reportVersionChange {
    group VERSIONING_GROUP
    shouldRunAfter gitCommit
    doLast {
        println "Current semver: ${config.version.major}.${config.version.minor}.${config.version.patch}"
    }
}
